plugins {
    id "java-library"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.kapt" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.plugin.allopen" version "${kotlinVersion}"
    id "com.github.ben-manes.versions" version "${gradleVersionCheckVersion}"
    id "io.micronaut.application" version "${micronautGradlePluginVersion}"
}

version = "${skeletonVersion}"
group = "hu.indicium.dev.services"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    kapt 'io.micronaut:micronaut-validation'
    kapt "io.micronaut:micronaut-inject-java"
    kapt "io.micronaut:micronaut-http-validation"
    annotationProcessor "io.micronaut.data:micronaut-data-processor"

    api "io.micronaut:micronaut-http-client"
    api "io.micronaut:micronaut-jackson-databind"
    api "io.micronaut:micronaut-runtime"
    api "io.micronaut:micronaut-validation"
    api "io.micronaut:micronaut-management"
    api "io.micronaut.security:micronaut-security"
    api "io.micronaut.security:micronaut-security-jwt"
    compileOnly "io.micronaut.kotlin:micronaut-kotlin-runtime"
    api "org.litote.kmongo:kmongo:$kmongoVersion"
    api "org.litote.kmongo:kmongo-coroutine:$kmongoVersion"

    api "io.micronaut.beanvalidation:micronaut-hibernate-validator"

    runtimeOnly "ch.qos.logback:logback-classic"

    annotationProcessor "io.micronaut:micronaut-http-validation"
    api "io.micronaut.mongodb:micronaut-mongo-reactive"
    api "io.micronaut.reactor:micronaut-reactor"
    api "io.micronaut.reactor:micronaut-reactor-http-client"
    api "jakarta.annotation:jakarta.annotation-api"

    api "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$kotlinxVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:$kotlinxVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinxVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.3.8"
}

application {
    mainClass.set("hu.indicium.dev.services.Application")
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("hu.indicium.dev.services.*")
    }
}

tasks {
    compileKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "skeleton"
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/svIndicium/$project.name"
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

jar {
    into("META-INF/maven/$project.group/$project.name") {
        from { generatePomFileForMavenJavaPublication }
        rename ".*", "pom.xml"
    }
}
