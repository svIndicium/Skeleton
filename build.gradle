plugins {
    id "java-library"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.kapt" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.plugin.allopen" version "${kotlinVersion}"
    id "com.github.ben-manes.versions" version "${gradleVersionCheckVersion}"
    id "io.micronaut.application" version "${micronautGradlePluginVersion}"
}

version = "${skeletonVersion}"
group = "hu.indicium.dev.services"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api 'org.projectlombok:lombok:1.18.22'
    annotationProcessor "org.projectlombok:lombok:1.18.22"

    kapt 'io.micronaut:micronaut-validation'
    kapt "io.micronaut:micronaut-inject-java"
    kapt "io.micronaut:micronaut-http-validation"
    kapt "io.micronaut.data:micronaut-data-processor"

    api "io.micronaut:micronaut-http-client"
    api "io.micronaut:micronaut-jackson-databind"
    api "io.micronaut:micronaut-runtime"
    api "io.micronaut:micronaut-validation"
    api "io.micronaut:micronaut-management"
    api "io.micronaut.data:micronaut-data-jdbc"
    compileOnly "io.micronaut.kotlin:micronaut-kotlin-runtime"

    api "io.micronaut.beanvalidation:micronaut-hibernate-validator"

    runtimeOnly "ch.qos.logback:logback-classic"
}


application {
    mainClass.set("hu.indicium.dev.services.Application")
}

java {
    sourceCompatibility = JavaVersion.toVersion("11")
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("hu.indicium.dev.services.*")
    }
}

tasks {
    compileKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "skeleton"
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/svIndicium/$project.name"
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

jar {
    into("META-INF/maven/$project.group/$project.name") {
        from { generatePomFileForMavenJavaPublication }
        rename ".*", "pom.xml"
    }
}
